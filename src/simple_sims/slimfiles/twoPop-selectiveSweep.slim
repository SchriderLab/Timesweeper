initialize()
{
	defineConstant("subpopCount", 2);
	defineConstant("subpopSize", 500);
	defineConstant("mutLoc", 50000);
	defineConstant("selCoeff", 0.05);
	
	//times
	defineConstant("burnTime", 20*subpopSize);
	defineConstant("splitTime", burnTime);
	defineConstant("migTime", burnTime+2*subpopSize);
	defineConstant("mutTime", migTime-1);
	defineConstant("softTime", mutTime+100);
	defineConstant("samplingTime", softTime);
	defineConstant("simEndTime", softTime+200);
	
	initializeMutationRate(1e-7);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 99999);
	initializeRecombinationRate(1e-7);
	
	// introduced mutation's initial selection coefficient
	if (sweep == "soft")
	{
		initializeMutationType("m2", 0.5, "f", 0);
	}
	else
	{
		initializeMutationType("m2", 0.5, "f", selCoeff);
	}
}

s1 500
{
	sim.addSubpopSplit("p2", subpopSize, p1);
	for (i in 2:subpopCount)
		sim.subpopulations[i-1].setMigrationRates(i-1, 0.0);
	for (i in 1:(subpopCount-1))
		sim.subpopulations[i-1].setMigrationRates(i+1, 0.0);
}

s2 1000 late()
{
	if (sweep == "hard" | sweep == "soft")
	{
		// save the state of the simulation	
		//cat("SAVING TO " + dumpFileName + "\n");
		sim.outputFull(dumpFileName);
	
		// introduce the sweep mutation
		target = sample(p2.genomes, 1);
		//cat("INTRODUCED MUTATION at gen " + sim.generation + " with 2Ns = " + 2*subpopSize*selCoeff + "\n");
		target.addNewDrawnMutation(m2, mutLoc);
	}
}

s3 1000:10000 late()
{
	if (sweep == "hard" | sweep == "soft")
	{
		fixed = (sum(sim.substitutions.mutationType == m2) == 1);
		
		if (fixed)
		{
			//cat(simID + ": FIXED at gen " + sim.generation + "\n");
			sim.deregisterScriptBlock(self);
		}
		else
		{
			muts = sim.mutationsOfType(m2);
			if (size(muts) == 0)
			{
				cat(simID + ": LOST at gen " + sim.generation + " - RESTARTING\n");
			
				// go back with a newly seeded attempt
				sim.readFromPopulationFile(dumpFileName);
				setSeed(rdunif(1, 0, asInteger(2^32) - 1));
			
				// re-introduce the sweep mutation
				target = sample(p2.genomes, 1);
				target.addNewDrawnMutation(m2, mutLoc);
			}
		}
	}
	else
	{
		sim.deregisterScriptBlock(self);
	}
}

s4 2000
{
    if (sweep == "soft")
    {
        muts = sim.mutationsOfType(m2);
        if (size(muts))
        {
                mut = sample(muts, 1);
                mut.setSelectionCoeff(selCoeff);
                //cat(simID + ": switched from neutral to beneficial at gen " + sim.generation + "\n");
        }
        else
        {
                //cat(simID + ": failed to switch from neutral to beneficial at gen " + sim.generation + "\n");
        }
    }
}

s5 10000 late()
{
	//cat("ALLOWING MIGRATION - " + sim.generation + "\n");
	for (i in 2:subpopCount)
		sim.subpopulations[i-1].setMigrationRates(i-1, 0);
	for (i in 1:(subpopCount-1))
		sim.subpopulations[i-1].setMigrationRates(i+1, 0);
}

s6 10000:15000 late()
{
	if (sweep == "hard" | sweep == "soft")
	{
		if (sim.countOfMutationsOfType(m2) == 0)
		{
			fixed = (sum(sim.substitutions.mutationType == m2) == 1);
			if (fixed)
			{
				//cat(simID + ": FIXED at gen " + sim.generation + "\n");
				sim.deregisterScriptBlock(self);
			}
		}
	}
}

s7 10000:15000 late()
{
	if ((sim.generation-samplingTime) % samplingInterval == 0)
	{
		if (sweep == "hard" | sweep == "soft")
		{
			muts = sim.mutationsOfType(m2);
			if (size(muts) > 0)
			{
				freq1 = sim.mutationFrequencies(p1, muts)[0];
				freq2 = sim.mutationFrequencies(p2, muts)[0];
				//cat("SEGREGATING IN p1 AND p2 at " + freq1 + " and " + freq2 + " respectively\n");
			}
			else
			{
				freq = "NO LONGER SEGREGATING at generation " + sim.generation + "; size(muts) == " + size(muts) + "\n";
			}
		}
	
		//cat("Sampling at generation " + sim.generation + "\n");
		pop1SampGenomes = sample(p1.genomes, subpopSize, replace=F);
		pop2SampGenomes = sample(p2.genomes, subpopSize, replace=F);
		fullSamp = c(pop1SampGenomes, pop2SampGenomes);
		fullSamp.outputSample(replace=F);
        //sim.subpopulations.outputMSSample(sampleSizePerStep1 + sampleSizePerStep2, filePath=outFileName, append=T);
		cat("Done emitting sample\n");
	}
}

s8 15000 late()
{
	sim.simulationFinished();
}

1 {
	// save this run's identifier, used to save and restore
	defineConstant("simID", getSeed());
	
	// set up our subpop
	sim.addSubpop("p1", subpopSize);

	//schedule our events
	sim.rescheduleScriptBlock(s1, start=splitTime, end=splitTime); //  splitting pop
	sim.rescheduleScriptBlock(s2, start=mutTime, end=mutTime); //  introducing mut
	sim.rescheduleScriptBlock(s3, start=mutTime, end=simEndTime); // checking on mut in p1
	sim.rescheduleScriptBlock(s4, start=softTime, end=softTime); // switching mut from neut to pos if necessary
	sim.rescheduleScriptBlock(s5, start=migTime, end=migTime); // allowing mig -- currently turned off for population-specific sweeps
	sim.rescheduleScriptBlock(s6, start=migTime, end=simEndTime); // reporting on fixation
	sim.rescheduleScriptBlock(s7, start=samplingTime, end=simEndTime); // sampling
	sim.rescheduleScriptBlock(s8, start=simEndTime, end=simEndTime); // wrapping up
}
